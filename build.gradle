// CCM Modding, Compresstion
buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies { 
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.0-SNAPSHOT' 
    }
}

apply plugin: "forge"

group = 'ccm.compresstion'
version = "0.0.1.${System.env.BUILD_NUMBER}"
archivesBaseName = 'Compresstion'

sourceSets {
    main {
        java { srcDir 'common' }
        resources { srcDir 'resources' }
    }
}

minecraft {
    version = "1.6.4-LATEST"
    assetDir = "run/assets"
}

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        expand version: version, mcversion: minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
    }
}

jar {
    appendix = 'universal'
    manifest {
        attributes 'MCVersion' : project.minecraft.version
        attributes 'Version' : project.version
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    appendix = 'src'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    appendix = 'dev'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    appendix = 'javadoc'
    from 'build/docs/javadoc'
}

artifacts {
    archives sourceJar 
	archives deobfJar 
    archives javadocJar
}